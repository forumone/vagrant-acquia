h1. apache Puppet Module

Based on "puppetlabs-apache":https://github.com/puppetlabs/puppetlabs-apache (C) PuppetLabs.


h2. Simple Usage

bc. include apache::ssl
# Realize previously defined virtual resources of Apache modules
Apache::Module <| |>

By default this will enable the following Apache modules:

* @rewrite@
* @headers@
* @expires@
* @deflate@
* @alias@
* @ssl@ (if including @apache::ssl@), configured to enable SNI (Server Name Indication)


h2. No SSL Support

To disable SSL support, simply @include apache@ instead of @include apache::ssl@.


h2. Common Usage

bc. include apache::ssl
package {
  libapache2-mod-fastcgi: ensure => present
}
# Enable other needed modules
@apache::module {
	actions:    ensure => present;
	fastcgi:    ensure => present;
	proxy_http: ensure => present;
}
# Realize previously defined virtual resources of Apache modules
Apache::Module <| |>


h2. Apache Modules Management

Example:

bc. @apache::module {
	fastcgi:    ensure => present;
	proxy_http: ensure => present;
	status:     ensure => absent;
}


h2. Standard VirtualHost

Useful for static pages such as maintenance pages:

bc.   apache::vhost { 'www.kreasiindonesia.com':
    serveradmin   => 'commerce-sysadmin+kreasiindonesia@bippo.co.id',
    serveraliases => ['kreasiindonesia.com', 'm.kreasiindonesia.com', 'www.m.kreasiindonesia.com'],
    docroot       => '/home/magento/www_maintenance',
  }

The default @Options@ directive is @FollowSymLinks MultiViews@.
@Indexes@ is disabled by default to improve security a bit.
The default @AllowOverride@ is @None@.


h2. FastCGI PHP

To connect to e.g. *PHP-FPM* :

bc. file { '/home/ceefour/public_html':
  ensure => directory,
  owner  => 'ceefour',
  group  => 'ceefour',
  mode   => 0755
}
file { '/home/ceefour/fastcgi-bin':
  ensure => directory,
  user   => 'ceefour',
  group  => 'ceefour',
  mode   => 0755
}
apache::vhost::fastcgi_php { 'ceefour.annafi.dev':
  docroot     => '/home/ceefour/public_html',
  fastcgi_dir => '/home/ceefour/fastcgi-bin',
  socket      => '/var/run/php-ceefour.sock',
  options     => 'All',
  require     => [ File['/home/ceefour/public_html'],
                   File['/home/ceefour/fastcgi-bin'] ]
}

The default @Options@ is @FollowSymLinks MultiViews@.
The default @AllowOverride@ is @All@.

Example PHP-FPM pool configuration (@/etc/php5/fpm/pool.d/ceefour.conf@) for the above Apache FastCGI PHP5 configuration:

bc. [ceefour]
listen = /var/run/php-ceefour.sock
user = ceefour
group = ceefour
pm = dynamic
pm.max_children = 10
pm.start_servers = 1
pm.min_spare_servers = 1
pm.max_spare_servers = 5

Or using "php Puppet module":https://github.com/soluvas/puppet-php:

bc.   file { "/home/${developer}/public_html":
    ensure => directory,
    owner  => $developer,
    group  => $developer,
    mode   => 0755
  }
  file { "/home/${developer}/fastcgi-bin":
    ensure => directory,
    owner  => $developer,
    group  => $developer,
    mode   => 0755
  }
  apache::vhost::fastcgi_php { "${developer}.${::fqdn}":
    docroot     => "/home/${developer}/public_html",
    fastcgi_dir => "/home/${developer}/fastcgi-bin",
    socket      => "/var/run/php-${developer}.sock",
    options     => 'All',
    require     => [ File["/home/${developer}/public_html"],
                     File["/home/${developer}/fastcgi-bin"] ]
  }


h2. Reverse Proxy

Used to proxy to Java Servers like "Geronimo":http://geronimo.apache.org, "JBoss AS":http://www.jboss.org/jbossas/, "GlassFish":http://glassfish.java.net/, "Tomcat":http://tomcat.apache.org/, etc.

Example:

bc.   apache::vhost::proxy { 'erp.bippo.co.id':
    path          => '/openbravo',
    dest          => 'http://192.168.50.109:8080/openbravo',
    serveraliases => ['www.erp.bippo.co.id'],
    serveradmin   => 'webmaster+erp@bippo.co.id',
  }

If you use *mod_jk*, it needs to be configured separately.


h2. Redirect

Default redirect status is @temporary@. Example configuration:

bc.   apache::vhost::redirect { 'plus.naylakidz.com':
    serveradmin   => 'commerce-sysadmin+naylakidz@bippo.co.id',
    serveraliases => ['www.plus.naylakidz.com', 'plus.naylakidz.net', 'www.plus.naylakidz.net'],
    dest          => 'https://plus.google.com/110822451617676222387',
    status        => permanent,
  }


h2. WebDAV Repository

bc.   apache::vhost::dav { "dav.${developer}.${::fqdn}":
    docroot     => "/home/${developer}/public_dav",
  }

Notable options and default values:

* @$options@        = '+Indexes', # DAV is usually publicly listable, unless you use it for normal hosting
* @$allow_override@ = 'None',
* @$allow_from@     = 'localhost', # By default it is "secure" because we don't put any security yet


h2. Custom Template for Magento Hosting

This reuses the @apache::vhost::fastcgi_php@ but with a different template:

bc.   apache::vhost::fastcgi_php { 'www.example.com':
  docroot     => '/home/magento/sites/example',
  fastcgi_dir => '/home/magento/fastcgi-bin',
  template    => 'apache/vhost-magento.conf.erb',
}

or with SSL:

bc.   apache::vhost::fastcgi_php { 'www.example.com':
  docroot     => '/home/magento/sites/example',
  fastcgi_dir => '/home/magento/fastcgi-bin',
  template    => 'apache/vhost-magento.conf.erb',
  ssl_cert    => '/etc/ssl/certs/www.example.com.crt',
  ssl_key     => '/etc/ssl/private/www.example.com.key',
  ssl_chain   => '/etc/ssl/certs/sub.class1.server.ca.pem',
  ssl_ca      => '/etc/ssl/certs/ca.pem',
}

It requires you to add your own @FastCgiExternalServer@, for example in @/etc/apache2/conf.d/php-magento@ :

bc. FastCgiExternalServer /home/magento/fastcgi-bin/php.external -socket /var/run/php-magento.sock -idle-timeout 900


h2. PHP-FPM via FastCGI plus Reverse Proxy

bc.  apache::vhost::fastcgi_php_proxy { "${developer}.${::fqdn}":
    docroot     => "/home/${developer}/public_html",
    fastcgi_dir => "/home/${developer}/fastcgi-bin",
    socket      => "/var/run/php-${developer}.sock",
    proxy_path  => '/commerce-center/',
    proxy_dest  => 'http://localhost:9080/commerce-center/',
    options     => 'All',
    require     => [ File["/home/${developer}/public_html"],
                     File["/home/${developer}/fastcgi-bin"] ]
  }


h2. C Library Development Usage

Add class @apache::dev@ :

bc. include apache::dev
